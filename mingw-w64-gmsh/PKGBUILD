# Maintainer: ImperatorS79 <fevrier.simon@gmail.com>

_realname=gmsh
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=4.7.1
_pkgver2=4_7_1
pkgrel=1
pkgdesc="This is Gmsh, an automatic three-dimensional finite element mesh generator with
built-in pre- and post-processing facilities."
arch=('any')
url="https://gmsh.info"
license=("GPLv2")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-make"
             "${MINGW_PACKAGE_PREFIX}-eigen3"
             "${MINGW_PACKAGE_PREFIX}-opencascade"
             "${MINGW_PACKAGE_PREFIX}-petsc"
             "${MINGW_PACKAGE_PREFIX}-fltk"
             "${MINGW_PACKAGE_PREFIX}-cairo"
             "${MINGW_PACKAGE_PREFIX}-gmp"
             "${MINGW_PACKAGE_PREFIX}-slepc")
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs")    
source=(https://gitlab.onelab.info/gmsh/gmsh/-/archive/gmsh_${_pkgver2}/gmsh-gmsh_${_pkgver2}.tar.gz
        0001-fix-link-lib-name.patch
        0002-fix-cairo-linking.patch
        0003-fix-opencascade-linking.patch)
sha256sums=('fcce5de7b4dea573d6cb8133339f62f8f73e23bc99df95ee58836c90eefe8102'
            'eefa684ece808fd705d5abb7804fc2494bdc0fd2b91da8d48378371ec9d6aa5c'
            'e21a8e889e4de1291d6b245fd459fd4742e3b830a8b8566357701a6fc658443a'
            '8aaeb6c330d712716bd91dd12c1fa3d077941fb6975d1ac41afe8899eda6f216')

prepare() {
  cd "${srcdir}/gmsh-gmsh_${_pkgver2}"
  patch -p1 -i "${srcdir}/0001-fix-link-lib-name.patch"
  patch -p1 -i "${srcdir}/0002-fix-cairo-linking.patch"
  patch -p1 -i "${srcdir}/0003-fix-opencascade-linking.patch"
}

build() {
  [[ -d ${srcdir}/build-${MINGW_CHOST} ]] && rm -rf ${srcdir}/build-${MINGW_CHOST}
  mkdir ${srcdir}/build-${MINGW_CHOST} && cd ${srcdir}/build-${MINGW_CHOST}
  
  local extra_flags
  if [ "$CARCH" = "i686" ]; then
	extra_flags=(
		-DENABLE_CAIRO=0
    )
  else
	extra_flags=(
		-DENABLE_CAIRO=1
    )
  fi
  
  echo $extra_flags
  
  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
    ${MINGW_PREFIX}/bin/cmake \
      -G"MSYS Makefiles" \
      -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
      -DCMAKE_BUILD_TYPE=Release \
      -DENABLE_BUILD_DYNAMIC=1 \
      -DENABLE_BUILD_SHARED=1 \
      -DENABLE_OCC=1 \
      -DENABLE_OCC_STATIC=1 \
      -DENABLE_OPENMP=1 \
      -DENABLE_OPENACC=1 \
      -DENABLE_PETSC=1 \
      -DENABLE_SLEPC=1 \
	  -DGMSH_RELEASE=1 \
	  "${extra_flags[@]}" \
      ../gmsh-gmsh_${_pkgver2}
  
  ${MINGW_PREFIX}/bin/cmake --build .
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  DESTDIR="${pkgdir}" ${MINGW_PREFIX}/bin/cmake --build . --target install
  
  mv "${pkgdir}${MINGW_PREFIX}"/lib/*.dll "${pkgdir}${MINGW_PREFIX}"/bin/
}
