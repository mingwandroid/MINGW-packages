# Maintainer: Lutz BÃ¼rkle <lutz.buerkle@web.de>

_realname=libebur128
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.2.6
pkgrel=1
pkgdesc="A Library that implements the EBU R 128 standard for loudness normalisation (mingw-w64)"
url="https://github.com/jiixyj/libebur128/"
arch=('any')
license=("MIT")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-cmake")
source=(https://github.com/jiixyj/${_realname}/archive/v${pkgver}.tar.gz
        "001-dont-link-m.patch")
sha256sums=('baa7fc293a3d4651e244d8022ad03ab797ca3c2ad8442c43199afe8059faa613'
            '5610fb33699276ac302d6bff03b5a1e9e9c4b0580edb2f85cb6eccbf51d75086')

prepare() {
  cd ${srcdir}/${_realname}-${pkgver}

  patch -p1 -i "${srcdir}/001-dont-link-m.patch"
}

build() {
  mkdir -p "${srcdir}/build-${MINGW_CHOST}"
  cd "${srcdir}/build-${MINGW_CHOST}"

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  ${MINGW_PREFIX}/bin/cmake \
    -G"MSYS Makefiles" \
    -DENABLE_INTERNAL_QUEUE_H=ON \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
    ../${_realname}-${pkgver}

  make
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR=${pkgdir} install
}
