# Maintainer: Sarah Ottinger <schalaalexiazeal@gmail.com>

_realname=ruamel-yaml
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python-${_realname}")
pkgver=0.16.13
pkgrel=1
pkgdesc='YAML parser/emitter that supports roundtrip preservation of comments, seq/map flow style, and map key order (mingw-w64)'
arch=('any')
url="https://sourceforge.net/projects/ruamel-yaml/"
license=('MIT')
depends=("${MINGW_PACKAGE_PREFIX}-python-ruamel.yaml.clib")
makedepends=("${MINGW_PACKAGE_PREFIX}-python-setuptools")
options=('!emptydirs')
source=("https://files.pythonhosted.org/packages/source/${_realname:0:1}/${_realname//-/.}/${_realname//-/.}-${pkgver}.tar.gz")
sha256sums=('bb48c514222702878759a05af96f4b7ecdba9b33cd4efcf25c86b882cef3a942')

prepare() {  
  cd "$srcdir"
  rm -rf python-build-${CARCH} | true
  cp -r "${_realname//-/.}-${pkgver}" "python-build-${CARCH}"
}

build() {
  msg "Python build for ${CARCH}"  
  cd "${srcdir}/python-build-${CARCH}"
  ${MINGW_PREFIX}/bin/python setup.py build
}

package() {
  cd "${srcdir}/python-build-${CARCH}"
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  RUAMEL_NO_PIP_INSTALL_CHECK=1 ${MINGW_PREFIX}/bin/python setup.py install \
  	--prefix=${MINGW_PREFIX} --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/python-${_realname}/LICENSE"
}
