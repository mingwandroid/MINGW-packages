# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>
# Contributor: nfnty <arch@nfnty.se>

_realname=shotcut
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgdesc='Cross-platform Qt based Video Editor'
pkgver=21.03.21
pkgrel=1
arch=('x86_64')
url='https://www.shotcut.org'
license=('GPL3')
depends=("${MINGW_PACKAGE_PREFIX}-qt5"
         "${MINGW_PACKAGE_PREFIX}-mlt"
         # "${MINGW_PACKAGE_PREFIX}-movit"
         "${MINGW_PACKAGE_PREFIX}-ffmpeg"
         # "${MINGW_PACKAGE_PREFIX}-libx264"
         "${MINGW_PACKAGE_PREFIX}-libvpx"
         "${MINGW_PACKAGE_PREFIX}-lame"
         "${MINGW_PACKAGE_PREFIX}-frei0r-plugins"
         # "${MINGW_PACKAGE_PREFIX}-ladspa"
         )
source=("$pkgname-$pkgver.tar.gz::https://github.com/mltframework/shotcut/archive/v$pkgver.tar.gz"
        "0001-include-path.patch"
        "0002-bin-path.patch"
        "0003-no-QT_STATIC.patch")
sha512sums=('402d714df7285d49269891e5def0b8a1a46f54b6ec88b0201981bdf8d866c49e83bbbf513805795630e39335e0fddc2457e1a7db582affe4a4d057ac5ab4e2bb'
            '482ec3b45852a1fe92419f4af07477ca1c7fdd7d358d2b14d41761703567bd20a9f5a30ee32e115679e53561862f13d6d1a8387b3820c33c1f6b9c74596102fe'
            'd89da07b0a7035ec5dcd77f71d7e49d063487e753a08b72513ad93903751edc3f921bfc4621a3e6d002c3062ec7a5e0d4b360318531f3d56811556e014e8d19b'
            '1ee2489e7dbc75d2c66d526e1363037446b2ccd2ba8d4af9fb04b9e1301d77dc045b70e504aa3e83c0716f217804bbf79726133487be6ac9ddb9a8434580074a')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"

  patch -p1 -i "${srcdir}/0001-include-path.patch"
  patch -p1 -i "${srcdir}/0002-bin-path.patch"
  # Broken:
  # ../x86_64-w64-mingw32/bin/ld.exe: release/newprojectfolder.o:newprojectfolder.cpp:(.text+0x1c76): undefined reference to `__imp__ZNK17CuteMessageLogger5writeEv'
  # .. and many more.
  # patch -p1 -i "${srcdir}/0003-no-QT_STATIC.patch"
}

build() {
  [[ -d ${srcdir}/build-${MINGW_CHOST} ]] && rm -rf ${srcdir}/build-${MINGW_CHOST}
  mkdir -p ${srcdir}/build-${MINGW_CHOST} && cd ${srcdir}/build-${MINGW_CHOST}
  export LLVM_INSTALL_DIR=${MINGW_PREFIX}
  export QTC_FORCE_CLANG_LIBTOOLING=1
  export QBS_INSTALL_DIR=${MINGW_PREFIX}
  if check_option "debug" "y"; then
    _config_variant=debug
  else
    _config_variant=release
  fi
      export MSYS2_ARG_CONV_EXCL="INSTALL_PREFIX="

  ${MINGW_PREFIX}/bin/qmake.exe \
    ../${_realname}-${pkgver}/shotcut.pro \
    CONFIG+=${_config_variant} \
    INSTALL_PREFIX="${MINGW_PREFIX}" \
    PREFIX="${MINGW_PREFIX}" \
    DEFINES+=SHOTCUT_NOUPGRADE \
    QMAKE_CFLAGS_RELEASE="$CFLAGS" \
    QMAKE_CXXFLAGS_RELEASE="$CXXFLAGS" \
    SHOTCUT_VERSION=msys2-mingw-w64-$pkgver \
    MLT_PATH=${MINGW_PREFIX}

  # INCLUDE_PATH+=${MINGW_PREFIX}/include/mlt++

  make ${_config_variant} \
    INSTALL_PREFIX="${MINGW_PREFIX}" \
    PREFIX="${MINGW_PREFIX}" \

  # lrelease ../translations/*.ts
}

package() {
  cd ${srcdir}/build-${MINGW_CHOST}
  # make DESTDIR="${pkgdir}" INSTALL_ROOT="${pkgdir}" install
    make \
      INSTALL_ROOT="${pkgdir}" \
      install
}
