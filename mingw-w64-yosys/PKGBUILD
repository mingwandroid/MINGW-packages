_realname=yosys
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.9.r10458.c718780ff
pkgrel=1
pkgdesc="A framework for RTL synthesis tools (mingw-w64)"
arch=('any')
license=("ISC")
url="http://www.clifford.at/yosys/"
groups=("${MINGW_PACKAGE_PREFIX}-eda")
depends=("${MINGW_PACKAGE_PREFIX}-ghdl"
         "${MINGW_PACKAGE_PREFIX}-python")
checkdepends=("${MINGW_PACKAGE_PREFIX}-iverilog")
makedepends=('flex'
             'git'
             "${MINGW_PACKAGE_PREFIX}-ghdl"
             "${MINGW_PACKAGE_PREFIX}-python")

_commit='c718780f'
source=("${_realname}::git://github.com/YosysHQ/${_realname}.git#commit=${_commit}"
        "ghdl-yosys-plugin::git://github.com/ghdl/ghdl-yosys-plugin.git#commit=6671d04"
        '0001-makefile-add-support-for-built-in-ghdl-yosys-plugin.patch')
sha256sums=('SKIP'
            'SKIP'
            '9383d2f0190a947533359258377c0203601b9d862b31820b68d8f27b70c7cd46')

pkgver() {
  cd "${_realname}"
  printf "0.9.r%s.%s" "$(git rev-list --count "${_commit}")" "$(git rev-parse --short "${_commit}")"
}

prepare() {
  cd "${srcdir}/${_realname}"
  patch -p1 < "${srcdir}"/0001-makefile-add-support-for-built-in-ghdl-yosys-plugin.patch
}

build() {
  cd "${srcdir}/${_realname}"

  if [ "$CARCH" = "i686" ]; then
    make config-msys2-32
  fi

  if [ "$CARCH" = "x86_64" ]; then
    make config-msys2-64
  fi

  mv "${srcdir}/ghdl-yosys-plugin"/src frontends/ghdl
  echo "ENABLE_GHDL=1" >> Makefile.conf
  echo "GHDL_DIR=${MINGW_PREFIX}" >> Makefile.conf

  make \
    GIT_REV="${_commit}" \
    PRETTY=0
}

check() {
  cd "${srcdir}/${_realname}"
  make test
}

package() {
  cd "${srcdir}/${_realname}"
  make DESTDIR="${pkgdir}" install
}
